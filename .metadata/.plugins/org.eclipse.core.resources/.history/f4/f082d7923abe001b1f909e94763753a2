buildscript {
	repositories {
		mavenCentral()
	}
	
	dependencies {
		classpath 'org.xtext:xtext-gradle-plugin:2.0.8'
	}
}


plugins {
	id "com.github.johnrengelman.shadow" version "7.0.0"
}


subprojects {
	ext.xtextVersion = '2.25.0'
	repositories {
		mavenCentral()
	}
	
	apply plugin: 'java'
	dependencies {
		implementation platform("org.eclipse.xtext:xtext-dev-bom:${xtextVersion}")
	}

	apply plugin: 'org.xtext.xtend'
	apply from: "${rootDir}/gradle/source-layout.gradle"
	apply plugin: 'eclipse'
	apply plugin: 'com.github.johnrengelman.shadow'
	
	group = 'org.xtext.calc'
	version = '1.0.0-SNAPSHOT'
	
	sourceCompatibility = '1.8'
	targetCompatibility = '1.8'
	
	configurations.all {
		exclude group: 'asm'
	}
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

shadowJar {
    baseName = 'calculator-language-server'
    classifier = null
    version = null
    manifest {
        attributes 'Main-Class': 'org.eclipse.xtext.ide.server.ServerLauncher'
    }
}